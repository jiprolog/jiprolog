/*
 * JIPKernel
 * JIProlog Kernel predicates
 *
 * 03/06/2004
 *
 * Copyright (C) 2002-2015 by Ugo Chirico. All Rights Reserved
 *
 * WARNING: DO NOT REMOVE THESE PREDICATES. THEY ARE REQUIRED BY JIPROLOG
 *
 */

'$system': \+ G :- call(G), !, fail.
'$system': \+ G.

'$system': not G :- call(G), !, fail.
'$system': not G.

'$system': ->(X,Y) :- call(X), !, Y.
'$system': *->(X,Y) :- call(X), Y.

%'$system': ;(X, _) :- X.
%'$system': ;(_, Y) :- Y.
%'$system': ;(*->(X,Y), _Z) :- X,Y.
%'$system': ;(*->(_X,_Y), Z) :- Z.

'$system': ^(X,Y) :- Y.

%one/1
'$system':one(X) :- call(X), !.

%once/1
'$system':once(X) :- call(X), !.

%ignore/1
'$system':ignore(X) :- call(X).
'$system':ignore(X) :-!.

%apply
'$system':apply(Term,Args):-
    Term =.. L1,
    append(L1,Args,L2),
    Goal =.. L2,
    !,
    Goal.

'$system':setup_call_cleanup(X,Y,Z):-
	once(X),
	call_cleanup(Y,Z).

% op/3 error checking code contributed by Paulo Moura
% and based on one of its contributions to XSB

'$system':op(Priority, Specifier, Operators) :-
	check_operator_priority(Priority),
	check_operator_specifier(Specifier),
	check_operator_names(Operators, Priority, Specifier),
	(	atom(Operators) ->
		add_operators([Operators], Priority, Specifier)
	;	add_operators(Operators, Priority, Specifier)
	).

add_operators([], _, _).
add_operators([Operator| Operators], Priority, Specifier) :-
	'$op'(Priority, Specifier, Operator),
	add_operators(Operators, Priority, Specifier).

check_operator_priority(Priority) :-
	(	var(Priority) ->
		error(instantiation_error)
	;	\+ integer(Priority),
		error(type_error(integer, Priority))
	;	(Priority < 0; Priority > 1200) ->
		error(domain_error(operator_priority, Priority))
	;	true
	).

check_operator_specifier(Term) :-
	(	var(Term) ->
		error(instantiation_error)
	;	\+ atom(Term) ->
		error(type_error(atom, Term))
	;	operator_specifier(Term, _) ->
		true
	;	error(domain_error(operator_specifier, Term))
	).

operator_specifier(fx,  prefix).
operator_specifier(fy,  prefix).
operator_specifier(xfx, infix).
operator_specifier(xfy, infix).
operator_specifier(yfx, infix).
operator_specifier(xf,  postfix).
operator_specifier(yf,  postfix).

check_operator_names(Term, Priority, Specifier) :-
	(	\+ ground(Term) ->
		error(instantiation_error)
	;	forbidden_operator(Term, Priority, Specifier, Permission) ->
		error(permission_error(Permission, operator, Term))
	;	atom(Term) ->
		true
	;	\+ is_list(Term) ->
		error(type_error(list, Term))
	;	\+ (member(Operator, Term), \+ check_operator_name(Operator, Priority, Specifier))
	).

forbidden_operator((','), _, _, modify) :-
	!.

forbidden_operator(('{}'), _, _, create) :-
	!.

forbidden_operator(('|'), Priority, Specifier, create) :-
	!,
	(	Priority \== 0, Priority \== 1105 ->
		true
	;	Specifier \== xfy
	).

forbidden_operator(Operator, _, Specifier, create) :-
	atom(Operator),
	(	operator_specifier(Specifier, infix),
		current_op(_, PostfixSpecifier, Operator),
		operator_specifier(PostfixSpecifier, postfix) ->
		true
	;	operator_specifier(Specifier, postfix),
		current_op(_, InfixSpecifier, Operator),
		operator_specifier(InfixSpecifier, infix) ->
		true
	;	fail
	).

check_operator_name(Term, Priority, Specifier) :-
	(	var(Term) ->
		error(instantiation_error)
	;	forbidden_operator(Term, Priority, Specifier, Permission) ->
		error(permission_error(Permission, operator, Term))
	;	atom(Term) ->
		true
	;	error(type_error(atom, Term))
	).

'$system':nonvar(X):- var(X), !, fail.
'$system':nonvar(X).

'$system':number(X):- (integer(X) ; float(X)), !.

'$system':string(X):- chars(X).

'$system':char(X):- chars([X]).

'$system':atomic(X):- (atom(X) ; number(X)), !.

'$system':callable(X):- (atom(X) ; compound(X)), !.

'$system':simple(X):- (atomic(X) ; var(X)), !.

'$system':compound(X):- (var(X);atomic(X)), !, fail.
'$system':compound(X).

'$system':list(X):-var(X), !, fail.
'$system':list([]).
'$system':list([_|T]):- list(T).

'$system':is_list(X):-var(X), !, fail.
'$system':is_list([]).
'$system':is_list([_|T]):- is_list(T).

'$system':list_or_partial_list(Var) :-
    var(Var),
    !.
'$system':list_or_partial_list([]).
'$system':list_or_partial_list([_| Tail]) :-
    list_or_partial_list(Tail).

'$system':nil([]).

%'$system':retractall(X):- retract(X), fail.
%'$system':retractall(_):-!.

'$system':current_predicate(X/Y):- !, current_functor(X, Y).
'$system':current_predicate(A):- error(type_error(predicate_indicator,A)).

'$system': @>(X, Y) :- compare(>, X, Y).

'$system': @<(X, Y) :- compare(<, X, Y).

'$system': @=(X, Y) :- compare(=, X, Y).

'$system': @>=(X, Y) :- compare(O, X, Y), (O == (>); O == (=)), !.

'$system': @=<(X, Y) :- compare(O, X, Y), (O == (<); O == (=)), !.

'$system': >=(X, Y) :- (X > Y ; X =:= Y), !.

'$system': =<(X, Y) :- (X < Y ; X =:= Y), !.

'$system': =\=(X, Y) :- \+ X =:= Y.

'$system': \==(X, Y) :- \+ X == Y.

'$system': =(X, X).

'$system': \=(X, Y):- \+ X = Y.

'$system':append([], L, L).
'$system':append([H|T], L, [H|R]) :-
	append(T, L, R).

'$system':member(X, [X|_]).
'$system':member(X, [_|T]) :-
    member(X, T).

%  member((?X, ?List, ?Pos)
'$system':member(X, [X|Xs], 1).
'$system':member(X, [Y|Ys], N):-
    '$system':member(X, Ys, N1),
    N is N1 + 1.

% arg/3
%'$system':arg(N, F, E):-
%	F =.. [_|X], member(E, X, N).

'$system':true.

'$system':false:-fail.

'$system':halt :- halt(0).

'$system':assertz(X) :-assert(X).

'$system': <!.

'$system':'C'([Terminal|Rest], Terminal, Rest).

'$system':phrase(T, Input):-'$kernel':dcg_rhs(T, S0, S, G), S0 = Input, S = [], G.

'$system':phrase(T, Input, Rest):-'$kernel':dcg_rhs(T, S0, S, G), S0 = Input, S = Rest, G.

%'$system':phrase(T, S):-phrase(T, S, []).

%'$system':phrase(T, S0, S):-'$kernel':dcg_rhs(T, S0, S, G), G.

'$system':expand_term(X, Y):-'$kernel':translate(X, Y).

'$system':unload(X):- unconsult(X).

'$system':use_module(File):-
    ensure_loaded(File).

'$system':include(File):-
    ensure_loaded(File).

'$system':compile(File) :-
	compile(File, []).

'$system':predicate_property(Head, Property):-
    predicate_properties(Head, Properties0),
	functor(Head, Functor, Arity),
	meta_predicate_template(Functor, Arity, Properties0, Properties),
    !,
    member(Property, Properties).

meta_predicate_template(Functor, Arity, Properties, [meta_predicate(Template)| Properties]) :-
	meta_predicate_template(Functor, Arity, Template),
	!.
meta_predicate_template(_, _, Properties, Properties).

% control
meta_predicate_template((\+), 1, \+ 0).
meta_predicate_template(catch, 3, catch(0,*,0)).
meta_predicate_template(once, 1, once(0)).
meta_predicate_template(not(_), not(0)).
meta_predicate_template(one(_), one(0)).
meta_predicate_template(ignore(_), ignore(0)).
meta_predicate_template(forall, 3, forall(0, 0)).
meta_predicate_template(call, N, Template) :-
	N > 0,
	functor(Template, call, N),
	Closure is N - 1,
	arg(1, Template, Closure),
	meta_predicate_call_n_template_args(N, Template).
meta_predicate_template(call_cleanup, 2, call_cleanup(0,0)).
meta_predicate_template(setup_call_cleanup, 3, setup_call_cleanup(0,0,0)).
% apply
meta_predicate_template(checklist, 2, checklist(1,*)).
meta_predicate_template(maplist, 3, maplist(2,*,*)).
% all solutions
meta_predicate_template(bagof, 3, bagof(*,0,*)).
meta_predicate_template(setof, 3, setof(*,0,*)).
meta_predicate_template(findall, 3, findall(*,0,*)).
meta_predicate_template(findall, 4, findall(*,0,*,*)).
% DCGs
meta_predicate_template(phrase, 2, phrase(2,*)).
meta_predicate_template(phrase, 3, phrase(2,*,*)).

meta_predicate_call_n_template_args(1, _) :-
	!.
meta_predicate_call_n_template_args(N, Template) :-
	arg(N, Template, *),
	N2 is N - 1,
	meta_predicate_call_n_template_args(N2, Template).

'$system':between(Lower, Upper, Integer) :-
 integer(Lower),
 integer(Upper),
 !,
 ( var(Integer) ->
  Lower =< Upper,
  '$kernel':generate_num(Lower, Upper, Integer)
 ; integer(Integer) ->
  Lower =< Integer,
  Integer =< Upper
 ; error(type_error(integer,Integer))
 ).

'$system':between(Lower, Upper, _) :-
 ( var(Lower) ->
  error(instantiation_error)
 ; var(Upper) ->
  error(instantiation_error)
 ; \+ integer(Lower) ->
  error(type_error(integer,Lower))
 ; % \+ integer(Upper) ->
  error(type_error(integer,Upper))
 ).

'$kernel':generate_num(Lower, _, Lower).
'$kernel':generate_num(Lower, Upper, Integer) :-
	Lower < Upper,
	Next is Lower + 1,
	'$kernel':generate_num(Next, Upper, Integer).


'$system':cyclic_term(T):-
    not acyclic_term(T).

/* Kernel Internal predicates */


'$kernel':translate(((LHS_in1, LHS_in2) --> RHS_in), (LHS_out:-RHS_out)):-
    nonvar(LHS_in1),
    !,
    tag(LHS_in1, S0, Sn, LHS_out),
    dcg_rhs(RHS_in, S0, S1, RHS_1),
    make_connects(LHS_in2, Sn, S1, Conn),
    dcg_and(RHS_1, Conn, RHS_2),
    flatten2(RHS_2, RHS_out).


'$kernel':translate((Module:LHS_in --> RHS_in), (Module:LHS_out:-RHS_out)):-
    !,
    translate((LHS_in --> RHS_in), (LHS_out:-RHS_out)).

'$kernel':translate((LHS_in --> RHS_in), (LHS_out:-RHS_out)):-
    nonvar(LHS_in),
    !,
    tag(LHS_in, S0, Sn, LHS_out),
    dcg_rhs(RHS_in, S0, Sn, RHS_1),
    flatten2(RHS_1, RHS_out).


'$kernel':translate(T, T).


'$kernel':dcg_rhs(X, S0, Sn, phrase(X, S0, Sn)):-
    var(X),
    !.

'$kernel':dcg_rhs((RHS_in1, RHS_in2), S0, Sn, RHS_out):-
    !,
    dcg_rhs(RHS_in1, S0, S1, RHS_out1),
    dcg_rhs(RHS_in2, S1, Sn, RHS_out2),
    dcg_and(RHS_out1, RHS_out2, RHS_out).

'$kernel':dcg_rhs((RHS_in1 -> RHS_in2), S0, Sn, (RHS_out1 -> RHS_out2)) :-
    !,
    dcg_rhs(RHS_in1, S0, S1, RHS_out1),
    dcg_rhs(RHS_in2, S1, Sn, RHS_out2).

'$kernel':dcg_rhs((RHS_in1 ; RHS_in2), S0, Sn, (RHS_out1 ; RHS_out2)):-
    !,
    dcg_or(RHS_in1, S0, Sn, RHS_out1),
    dcg_or(RHS_in2, S0, Sn, RHS_out2).

'$kernel':dcg_rhs({RHS_in}, S0, Sn, (RHS_in, (S0 = Sn))):-
    !.

'$kernel':dcg_rhs({}(RHS_in), S0, Sn, (RHS_in, (S0 = Sn))):-
    !.

'$kernel':dcg_rhs(!, S0, Sn, (!, (S0 = Sn))):-
    !.

'$kernel':dcg_rhs(\+ RHS_in, S0, Sn, (\+ Goal, (S0 = Sn))) :-
	!,
	dcg_rhs(RHS_in, S0, _, Goal).

'$kernel':dcg_rhs(call(RHS_in), S0, Sn, call(RHS_in, S0, Sn)) :-
	!.

'$kernel':dcg_rhs([], S0, Sn, (S0 = Sn)) :-
	!.

'$kernel':dcg_rhs([T| Ts], S0, Sn, Goal) :-
	!,
	make_connects([T| Ts], S0, Sn, Goal).

'$kernel':dcg_rhs(Module:RHS_in, S0, Sn, Module:RHS_out):-
	!,
    tag(RHS_in, S0, Sn, RHS_out).

'$kernel':dcg_rhs(RHS_in, S0, Sn, RHS_out):-
    tag(RHS_in, S0, Sn, RHS_out).


'$kernel':dcg_or(In, S0, Sn, Out):-
    dcg_rhs(In, S1, Sn, Out1),
    ( var(S1),
      \+ S1 == Sn,
      !,
      S0 = S1,
      Out = Out1 ;
      Out = (S0 = S1, Out1)).


'$kernel':dcg_and(true, In, In):-
    !.

'$kernel':dcg_and(In, true, In):-
    !.

'$kernel':dcg_and(In1, In2, (In1, In2)).


'$kernel':tag(In, S0, Sn, Out):-
	In =.. [Predicate| Arguments],
	append(Arguments, [S0, Sn], NewArguments),
	Out =.. [Predicate| NewArguments].


'$kernel':flatten2(In, In):-
    var(In),
    !.

'$kernel':flatten2((In1, In2), Out1):-
    !,
    flatten1(In1, Out1, Out2),
    flatten2(In2, Out2).

'$kernel':flatten2(In, In).

'$kernel':flatten1(In1, (In1, In2), In2):-
    var(In1),
    !.

'$kernel':flatten1((In1, In2), Out1, In3):-
    !,
    flatten1(In1, Out1, Out2),
    flatten1(In2, Out2, In3).

'$kernel':flatten1(In1, (In1, In2), In2).

'$kernel':make_connects(Terminals, S0, Sn, Conns):-
	(	list(Terminals) ->
		append(Terminals, Sn, List),
		Conns = (S0 = List)
	;	list_or_partial_list(Terminals),
		Conns = append(Terminals, Sn, S0)
	).
